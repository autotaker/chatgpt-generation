---
tags: Haskell ghc GarbageCollection
title: GHC/RTSのGCについて - 1
---
Haskellで効率の良いコードを書くためにはいかに不要なサンクを潰すか、ということが重要だと言われています。しかし、そもそもなぜサンクが増えると効率が悪くなるのでしょうか。

# Haskellのメモリ確保は高速
まず、Haskellにおいてメモリの確保はどの程度コストがかかるものなのでしょうか。次のプログラムを使って確かめてみましょう。

```haskell
{-# LANGUAGE BangPatterns #-}
{-# OPTIONS_GHC "-ddump-simpl" #-}
module Main2 where
bench :: Int -> (a -> a) -> a -> a
bench n f i = go n i
    where
    go 0 !i = i
    go k !i = go (k-1) (f i)
{-# NOINLINE bench #-}

main :: IO ()
main = print (bench 100000000 (\x -> x + 1 :: Int) 0)
```

このプログラムはちょうど1億個のInt型の値を生成するようにデザインされています。このプログラム(heap-light)を実行して実行の統計情報を取ってみましょう。

```
stack exec heap-light -- +RTS -s 
100000000
   1,600,051,368 bytes allocated in the heap
          62,504 bytes copied during GC
          44,576 bytes maximum residency (2 sample(s))
          29,152 bytes maximum slop
               0 MB total memory in use (0 MB lost due to fragmentation)

                                     Tot time (elapsed)  Avg pause  Max pause
  Gen  0      1524 colls,     0 par    0.003s   0.003s     0.0000s    0.0000s
  Gen  1         2 colls,     0 par    0.000s   0.000s     0.0001s    0.0001s

  INIT    time    0.000s  (  0.000s elapsed)
  MUT     time    0.474s  (  0.476s elapsed)
  GC      time    0.003s  (  0.003s elapsed)
  EXIT    time    0.000s  (  0.000s elapsed)
  Total   time    0.477s  (  0.479s elapsed)

  %GC     time       0.0%  (0.0% elapsed)

  Alloc rate    3,373,401,097 bytes per MUT second

  Productivity  99.4% of total user, 99.4% of total elapsed
```
0.474秒で実行できました。Alloc rateの欄に書かれていますが、これはおよそ3.38GB/sの速さでメモリを確保できているということであり、十分な速さであると考えられます。比較としてCのメモリ確保（malloc/free)と比べてみましょう。

```c

#include <stdio.h>
#include <stdlib.h>

int main(void)
{
    int i, n;
    n = 100000000;
    long long* ptr = malloc(16);
    for( i = 0; i < n; i++) {
        free(ptr);
        ptr = malloc(16);
        ptr[1] = i;
    }
    printf("%lld\n", ptr[1]);
    free(ptr);
    return 0;
}
```
多少不自然なプログラムになっているのはgccの最適化を欺くためです。（ループ内でmallocとfreeを行うコードを書いたら最適化でループごと消去されてしまったので）
このプログラムを実行すると0.834秒かかりました。すなわち、C言語よりも**1.8倍程度速い**ということになります。

```bash
gcc -O main.c && time ./a.out
99999999

real	0m0.834s
user	0m0.833s
sys	0m0.000s
```

# HaskellのGCが遅い
ではなぜHaskellのプログラムは遅くなるのでしょうか。
先程の例では本質的に必要なメモリは再帰関数の引数`i`の2wordsだけであるので、より多くのメモリを必要とするプログラム(heap-heavy)で実験してみましょう。

```haskell
{-# LANGUAGE BangPatterns #-}
{-# OPTIONS_GHC "-ddump-simpl" "-fspec-constr" #-}
module Main where
import Debug.Trace
import qualified Data.Vector as V
import System.Environment

bench :: Int -> Int -> V.Vector Int
bench n i = V.unfoldrN n (\ !x -> Just (x, x+1)) i

main :: IO ()
main = do
    [n] <- fmap (map read) getArgs
    let k = 100000000 `div` n
    putStrLn $ "iteration: " ++ show k
    let go i !x | i == 0 = print $ V.last x
                | otherwise = traceMarker "tick" $ go (i - 1:: Int) (bench n i)
    go k (bench n 0)
```
このプログラムは標準入力から整数`n`を受け取り、「長さ`n`のInt型の配列を生成する」という手続きを`100000000/n`回だけ繰り返します。したがって、合計で3 * 100M words = 2.4G byteのメモリを確保します。また、再帰関数`go i x`の実行中は長さ`n`の配列を開放できないので、常に`n * 3` wordsのメモリを必要とするプログラムになっています。このプログラムの`n`を1000から4,096,000まで２倍ずつ変化させて実行時間をグラフにしてみましょう。

![消費メモリと実行時間.png](https://qiita-image-store.s3.amazonaws.com/0/67228/e1816fb0-c955-2571-73e1-d9f60a293961.png)

MUTはプログラム本体の計算に必要としたCPU時間で、GCはガーベッジコレクションに要したCPU時間です。ここからわかるようにMUTの時間はほとんど変わりませんが、`n`が64Kを超えたあたりから急激にGCの時間が増えて、実行時間も遅くなっています。

この挙動を理解するためにはGHCのランタイムシステムとそこで行われているガーベッジコレクションの仕組みについて理解する必要があります。

# GHCの世代別GC

GHCでは（デフォルトで）以下の３つの世代にヒープオブジェクトを分けて確保します。

* **Nursery blocks**: Nurseryとは託児所のことで、新しく作られたヒープオブジェクトはまずここに確保されます。nursery blocksは各物理スレッドに1列ずつ作られます。このため、Haskellでは複数のスレッドが同時にメモリを確保してもロックせずに済みます。
* **Gen-0 blocks**: 比較的若いヒープオブジェクトが住まう領域です。グローバルに１列のブロックとなっています。
* **Gen-1 blocks**: 長く生きるヒープオブジェクトが住まう領域です。これもグローバルに１列のブロックになります。

## Aging (成長）/Promotion
世代別GCでは各ヒープオブジェクトはGCが走るたびに年を取ります。これをAgingまたはPromotionと呼びます。年を取ると、そのヒープオブジェクトは次の世代のブロックに移動します。つまり、Nurseryに住んでいたオブジェクトはGen-0に移動し、Gen-0に住んでいたオブジェクトはGen-1に移動します。Gen-1に住んでいるオブジェクトはもう年を取りません。

## 世代別GC
GHCではMinor GCとMajor GCという2種類のGCが行われます。

* **Minor GC**ではNusery blocksとGen-0 blocksのオブジェクトがGCされます。
* **Major GC**ではすべてのブロックのオブジェクトがGCされます。
どちらのGCもデフォルトではCopy GCが走るようです。

## いつGCが起こるか
あるスレッドでnursery blocksを使い果たすと、HeapOverflowとなり、GCがトリガされます。この際、Gen-1 blocksの長さがしきい値を超えているとMajor GCが走ります。超えていない場合はMinor GCが走ります。このしきい値は前回のGCで生き残ったオブジェクトのブロック数にFactor(`+RTS -F`で指定できる、デフォルト値2)をかけたものになります。

nursery blocksのサイズは`+RTS -A`で指定できます。現在のデフォルト値は1M byteです。

## GCの計算時間
GCにかかる時間のほとんどは生きているヒープオブジェクトを次の世代にコピーするのに費やされます。したがって、**各世代の生きているオブジェクトのサイズ** に比例した実行時間がかかります。たとえ各世代に大量のオブジェクトがあったとしても、そのほとんどが死んでいる場合にはGCはほとんど時間を消費しません。
